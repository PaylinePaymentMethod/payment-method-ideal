
repositories {
    mavenLocal()
    maven {
        url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-public/"
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
    maven {
        url "https://nexus.intra.monext.fr:10130/nexus/content/groups/public"
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
    maven {
        url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-snapshot/"
        credentials {
            username mavenUser
            password mavenPassword
        }
    }
}

ext {
    releaseDirectoryPath = 'D:/build'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            from project.shadow.component(publication)
            // On modifie l'artifactId pour qu'il correspond au CARDCODE et soit publié dans le bon repo pour etre utiliser via le CA...
            artifactId = 'payment-method-ideal-v2'
            groupId 'com.payline.payment.ideal-v2'
        }
    }
    repositories {
        maven {
            afterEvaluate {
                if (project.version.endsWith('-SNAPSHOT')) {
                    url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-snapshot/"
                } else {
                    url "https://nexus.intra.monext.fr:10130/nexus/content/repositories/jindus-repo-pay-pay-public/"
                }
                credentials {
                    username mavenUser
                    password mavenPassword
                }
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

plugins.withType(JacocoPlugin) {
    tasks["test"].finalizedBy 'jacocoTestReport'
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
        property 'sonar.dependencyCheck.htmlReportPath', "${project.buildDir}/reports/dependency-check-report.html"
    }
}

task buildToLocal(type: Copy) {
    dependsOn shadowJar
    from shadowJar
    into "${releaseDirectoryPath}"
}

task printVersion {
    doLast {
        println version
    }
}